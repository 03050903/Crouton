/*
 * Copyright 2013 Benjamin Weiss
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'android-library'
apply plugin: 'android-maven'
apply plugin: 'signing'

dependencies {
  compile 'com.android.support:support-v4:18.0.+'
}

android {
  buildToolsVersion '19'
  compileSdkVersion 19
  defaultConfig {
    versionCode 5
    versionName "1.8.2"
    minSdkVersion 4
    targetSdkVersion 19
  }
}

group = "de.keyboardsurfer.android.widget"
artifact = "crouton"
version = "1.8.2"

android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (name.equals(com.android.builder.BuilderConstants.DEBUG)) {
    return; // Skip debug builds.
  }
  def task = project.tasks.create "jar${name.capitalize()}", Jar
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  artifacts.add('archives', task);
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//  classifier = 'javadoc'
//  from android.javadoc.destinationDir
//}

artifacts {
  archives sourcesJar
}

task wrapper(type: Wrapper) {
  gradleVersion = "1.9"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUser, password: sonatypePass)
      }

      pom.project {
        name 'crouton'
          packaging 'jar'
          packaging 'aar'
          description 'Context sensitive notifications for Android'
          url 'https://github.com/keyboardsurfer/Crouton'

          scm {
            url 'scm:git@github.com:keyboardsurfer/Crouton.git'
              connection 'scm:git@github.com:keyboardsurfer/Crouton.git'
              developerConnection 'scm:git@github.com:keyboardsurfer/Crouton.git'
          }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
          }
        }

        developers {
          developer {
            id 'keyboardsurfer'
              name 'Benjamin Weiss'
          }
        }
      }
    }
  }
}
